//YM_SRBUVTransfer - ´«µÝUV
//srbUVTransfer.mel - MEL Script
//Copyright (C) Suchan Raj Bajracharya
//Title: SRB - SRB - UV Transfer v0.1
//AUTHOR:	Suchan Raj Bajracharya
//				suchan@incessantrain.com
//				srb_yagya@hotmail.com		
//VERSION: 0.1
//DATE: May 6th, 2009
//DESCRIPTION: Use this Tool to automatically transfer the UV sets of multiple objects...
//USAGE: Load the source objects and the target objects and click apply...
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//CREATE THE GLOBAL PROCEDURE FOR srbUVTransfer.mel---
global proc srbUVTransfer () {

//User Interface---------------------

if (`window -exists srbUVTransferWin`) deleteUI srbUVTransferWin;
window
	-rtf 1
	-title "SRB - UV Transfer v0.1"
srbUVTransferWin;

columnLayout -adj 1;
	
rowLayout
	-numberOfColumns 2
	-columnWidth2 200 200
	-columnAttach 1 "both" 5
	-columnAttach 2 "both" 5;

//SOURCE LIST
	columnLayout
		-adjustableColumn true
		-columnAlign "center";
	
	text
		-label "Source Objects";

 textScrollList
 	 -numberOfRows 20
	 -allowMultiSelection false			
 srbUVSourceTSL;

setParent..;

//TARGET LIST
	columnLayout
	 -adjustableColumn true
	-columnAlign "center";
	
 text
	-label "Target Objects";

textScrollList
	-numberOfRows 20
	-allowMultiSelection true
	srbUVTargetTSL;

setParent..;
setParent..;

rowLayout
	-numberOfColumns 2
	-columnWidth2 200 200
	-columnAttach 1 "both" 5
	-columnAttach 2 "both" 5;
	
button -bgc 0.3 0.5 0.7 -l "Add Source Objects" -c "srbUVAddToList(0)";
button -bgc 0.3 0.5 0.7 -l "Add Target Objects" -c "srbUVAddToList(1)";

setParent..;

rowLayout
	-numberOfColumns 2
	-columnWidth2 200 200
	-columnAttach 1 "both" 5
	-columnAttach 2 "both" 5;
	
button  -bgc 0.75 0.3 0.3  -l "Remove All Source Objects" -c ("textScrollList -edit -removeAll srbUVSourceTSL");
button  -bgc 0.75 0.3 0.3 -l "Remove All Target Objects" -c ("textScrollList -edit -removeAll srbUVTargetTSL");

//SET PARENT TO THE MAIN LAYOUT---
setParent..;
button  -bgc 0.3 0.75 0.3 -l "Apply" -c "srbUVTransferProc";

showWindow srbUVTransferWin;
}

//GLOBAL PROCEDURE FOR srbUVAddToList---
global proc srbUVAddToList (int $choice) {
string $srbSelObjs[] = `ls -sl`;
if ($choice == 0)
{
	for ($objs in $srbSelObjs)
	{
		textScrollList -edit -append $objs srbUVSourceTSL;
	}
}
else
{
	for ($objs in $srbSelObjs)
	{
		textScrollList -edit -append $objs srbUVTargetTSL;
	}
}
}

//GLOBAL PROCEDURE FOR srbUVTransferProc---
global proc srbUVTransferProc ()
{
	string $srbSourceObjectsList[] = `textScrollList -query -allItems	srbUVSourceTSL`;
	string $srbTargetObjectsList[] = `textScrollList -query -allItems	srbUVTargetTSL`;
	int $srbSizeSourceObjs = `size $srbSourceObjectsList`;
	int $srbSizeTargetObjs = `size $srbSourceObjectsList`;

	if ($srbSizeSourceObjs == $srbSizeTargetObjs)
	{
		for ($i = 0; $i < $srbSizeSourceObjs; $i++)
		{
			polyTransfer -v 0 -vc 0 -uv 1 -ao $srbSourceObjectsList[$i] $srbTargetObjectsList[$i];
		}
		
		print "SRB - UV Transfer v0.1 has sucessfully transferred the UV...\n";
	}
	else
	{
		warning "SRB - UV Transfer v0.1 has encounterred a problem. Source object is not equal to target objects...terminating...\n";
	}
}
srbUVTransfer;
//THE END---


