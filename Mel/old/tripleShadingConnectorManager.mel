//获取链接到inShape上的上游shape节点
global proc string connectionShape(string $tripeNode,int $index)
{
    string $srcInst = `connectionInfo -sfd ( $tripeNode + ( ".input[" + $index + "].inShape" ))`;
    string $temps[] = {};
    tokenize $srcInst "." $temps;
    return $temps[0];
}


//列出所有链接的shape
global proc string [] tripleList(string $tripleNode)
{
    int $tripleCounts[] = `getAttr -mi ( $tripleNode + ".input" )`;//这个input可以间隔存在，比如没有3也可以有5，6
    string $tripleList[] = {};
    for($index in $tripleCounts)
    {
        $tripleList[size($tripleList)] = connectionShape($tripleNode,$index);
        //$tripleList[size($tripleList)] = connectionInfo($tripleNode,$i);
    }
    return $tripleList;
}



//查看是否shape已经链接倒triple
global proc int isConnectToTriple(string $obj,string $tripleNode)
{
    return (stringArrayContains($obj,tripleList($tripleNode)));
}


global proc connectObjToTriple(string $obj,string $tripleNode,int $index,int $findNull)
{ 
    if($findNull>0)
    {
        if(`connectionInfo -id ($tripleNode+".input["+ $index +"].inShape")`)
        {
            $index += 1;
            connectObjToTriple($obj,$tripleNode,$index,1);
        }   //(自动填空)给新加的物体找一个空链接,使用了递归,要加载对象过多的话会比较卡,
            //第一次添加可以用不递归,之后再修改就需要查空了。
        else if (isConnectToTriple($obj,$tripleNode)==0)
        {
            catchQuiet(`connectAttr -f ( $obj + ".instObjGroups[0]" ) ( $tripleNode+".input[" + $index + "].inShape" )`);
            //链接物体的instObjGroup到tripleShadingSwitch的input.inShape
        }
    }
    else
    {
        if(isConnectToTriple($obj,$tripleNode)==0)
        {
            catchQuiet(`connectAttr -f ( $obj + ".instObjGroups[0]" ) ( $tripleNode+".input[" + $index + "].inShape" )`);
            //链接物体的instObjGroup到tripleShadingSwitch的input.inShape
        }
    }
}

//查找物体在TripleNode中对应的index
global proc int findIndexInTriple(string $obj,string $tripleNode)
{
    if(isConnectToTriple($obj,$tripleNode)>0)
    {
        string $listConnections[] = `connectionInfo -dfs ($obj + ".instObjGroups[0]")`;  //获取所选物体的instObjGroups[0]所连接的属性
        string $temp[] = {};
        for($connection in $listConnections)
        {
            int $findTriple = `gmatch $connection ($tripleNode+"*")`;  //匹配输入的TripleNode
            if($findTriple>0)
            {   
                tokenize $connection ".[]" $temp;  //析出字符串的数字
            }
        }
        return ($temp[2]);
    }
    else
    {
        return -1;  //没有则返回-1,避免返回0
    }
}


//打断选择的物体与Triple的连接
global proc disconnectShape(string $obj,string $tripleNode)
{
    int $objIndex = findIndexInTriple($obj,$tripleNode);
    if($objIndex>=0)
    {
        disconnectAttr ($obj+".instObjGroups[0]") ($tripleNode+".input["+$objIndex+"].inShape");
        string $getMat = `connectionInfo -sfd ($tripleNode+".input["+$objIndex+"].inTriple")`;
        if(size($getMat)>0)
        {
            disconnectAttr ($getMat) ($tripleNode+".input[" + $objIndex+ "].inTriple");
        }
    }
}
//连接材质球到所选择的物体的后面
global proc connectMat(string $obj,string $tripleNode,string $mat)
{
    int $objIndex = findIndexInTriple($obj,$tripleNode); //查找输入物体的index
    if($objIndex >= 0)
    {
        string $tripleConnection = `connectionInfo -sfd ($tripleNode + ".input["+$objIndex+"].inTriple")`;
        if(size($tripleConnection) == 0)  //查询是否已经有材质连接到对应的物体上
        {
            if(`attributeExists "outColor" $mat`)
            {
                connectAttr -f ($mat+".outColor") ($tripleNode+".input["+$objIndex+"].inTriple");
            }
            else
            {
                warning("请检查 \"" + $mat + "\"是否有\"outColor\"属性\n");
            }
        }
        else
        {
            disconnectAttr ($tripleConnection) ($tripleNode+".input["+$objIndex+"].inTriple");
            if(`attributeExists "outColor" $mat`)
            {
                connectAttr -f ($mat+".outColor") ($tripleNode+".input["+$objIndex+"].inTriple");
            }
            else
            {
                warning("请检查 \"" + $mat + "\"是否有\"outColor\"属性\n");  
            }
        }
    }
    else
    {
        //如果输入的物体没有连接到TripleNode则警告:
        warning ("请检查 \""+$obj+" \"是否连接到 \""+$tripleNode+" \";\n");
    }
}

//connectMat("pCubeShape16","nihaoTripleSwitch","lambert1");
//attributeExists "outColor" "surfaceShader1";
//删除所选的triple节点
global proc delTripleNode()
{
    global string $tripleScrollList;
    string $nodeName[] = `textScrollList -q -si $tripleScrollList`;
    lockNode -l off $nodeName[0];
    delete $nodeName[0];
    listNode("triple");
}

global proc addSelToTriple()
{
	global string $objScrollList, $tripleScrollList, $matScrollList;
    string $objList[] = `ls -sl -g -dag -ni`;
    string $tripleNode[] = `textScrollList -q -si $tripleScrollList`;
    for($i=0;$i<size($objList);$i++)
    {
        connectObjToTriple($objList[$i],$tripleNode[0],$i,1);
    }
}

global proc disconnectSelFromTriple()
{
	global string $objScrollList, $tripleScrollList, $matScrollList;
    string $objList[] = `ls -sl -g -dag -ni`;
    string $tripleNode[] = `textScrollList -q -si $tripleScrollList`;
    for($i=0;$i<size($objList);$i++)
    {
        disconnectShape($objList[$i],$tripleNode[0]);
    }
}

global proc addMatToSel()
{
	global string $objScrollList, $tripleScrollList, $matScrollList; 
    //string $objList[] = `ls -sl -g -dag -ni`;
	string $tripleNode[] = `textScrollList -q -si $tripleScrollList`;
	string $mat[] = `textScrollList -q -si $matScrollList`;
	string $objList[] = `textScrollList -q -si $objScrollList`;
    for($i=0;$i<size($objList);$i++)
    {
        connectMat($objList[$i],$tripleNode[0],$mat[0]);
    }
}

//创建AOV，必须先加载mtoa并且保证当前渲染层为arnold
global proc createAOV()
{
    global string $tripleScrollList;
	global string $prefixName;
	int $type = aovType();
	$aovName = `textFieldGrp -q -tx $prefixName`;   //获取手动输入的前缀
	if(`pluginInfo -q -l "mtoa"`)
	{
	    if(!`objExists ("aiAOV_"+$aovName)`)
	    {
            python("from mtoa import aovs\n"+
    				"aovs.AOVInterface().addAOV(\'"+$aovName+"',aovType="+$type+")");
    				
    		string $tripleNode = `shadingNode -au -n ($aovName+"_TripleSwitch") "tripleShadingSwitch"`;	  //以aov的名字创建triple
    		connectAttr -f ($tripleNode+".output") ("aiAOV_"+$aovName+".defaultValue");   //连接Triple到AOV
    		setAttr ($tripleNode+".default") -type double3 0 0 0;   //设置默认色为黑色
    		listNode("triple");
    		textScrollList -e -si ($aovName+"_TripleSwitch") $tripleScrollList;   
		}
		else
		{
		    warning ("已经有 \"" + $aovName + "\"这个通道了");
		}
	}
	else
	{
		warning("请加载\"mtoa!\n");
	}
}


//将选择的物体列出在列表中
global proc listNode(string $listType)
{
	global string $objScrollList,$tripleScrollList,$matScrollList;
	switch($listType)
	{	
		case "shape":
		{	
			string $objList[] = `ls -sl -g -dag -ni`;
			textScrollList -e -ra $objScrollList;
			for($obj in $objList)
			{	
				textScrollList -e -a $obj $objScrollList;
			}
			break;
		}
		case "mat":
		{
			string $matList[] = `ls -sl -mat`;
			textScrollList -e -ra $matScrollList;
			for($mat in $matList)
			{
				textScrollList -e -a $mat $matScrollList;
			}
			break;
		}
		case "triple":
		{
			string $tripleList[] = `ls -typ "tripleShadingSwitch"`;
			textScrollList -e -ra $tripleScrollList;
			for($triple in $tripleList)
			{
				textScrollList -e -a $triple -sii 1 $tripleScrollList;
			}
			break;
		}
		default :
			print ("b(￣￣)d");
			break;
	}
}

//双击列表选择
global proc doubleClickInListCmd(string $list)
{
    global string $objScrollList,$tripleScrollList,$matScrollList;
    switch($list)
    {
        case "shape":
        {
            string $selItem[] = `textScrollList -q -si $objScrollList`;
            select -r $selItem;
            //ShowAttributeEditorOrChannelBox;
            break;
        }
        case "triple":
        {
            string $selItem[] = `textScrollList -q -si $tripleScrollList`;
            select -r $selItem;
            ShowAttributeEditorOrChannelBox;
            break;
        }
        case "mat":
        {
            string $selItem[] = `textScrollList -q -si $matScrollList`;
            select -r $selItem;
            ShowAttributeEditorOrChannelBox;  //打开属性编辑器
            break;
        }
        default:
            print ("b(￣￣)d");
			break;
    }
}

//选择AOV类型
global proc int aovType()
{
    global string $typeMenu;
    string $aovTypeLists = `optionMenu -q -v $typeMenu`;
    int $aovTypeIndex = 5;

    switch ($aovTypeLists)
    {
        case "int":
            $aovTypeIndex = 1;
            break;
        case "bool":
            $aovTypeIndex = 3;
            break;
        case "float":
            $aovTypeIndex = 4;
            break;
        case "rgb":
            $aovTypeIndex = 5;
            break;
        case "rgba":
            $aovTypeIndex = 6;
            break;
        case "vector":
            $aovTypeIndex = 7;
            break;
        case "point":
            $aovTypeIndex = 8;
            break;
        case "point2":
            $aovTypeIndex = 9;
            break;
        case "pointer":
            $aovTypeIndex = 11;
            break;
        default:
            $aovTypeIndex = 5;
            break;
    }
    return $aovTypeIndex;
}



global proc tripleShadingConnectorManager()
{
    global string $objScrollList, $tripleScrollList, $matScrollList,$prefixName,$typeMenu;
    if(`window -ex tripleShadingConnector`)
    {
    	deleteUI tripleShadingConnector;
    	windowPref -r tripleShadingConnector;
    }
    
    window -rtf 1 -s 1 -t "TripleShadingConnector" -w 778 -h 300 tripleShadingConnector;
    columnLayout;
    rowLayout -nc 3;
    $prefixName = `textFieldGrp -cal 1 "left" -l "AOV Name: " -w 500`;
    $typeMenu = `optionMenu -l "AOV Type: "`;
        menuItem -label "int";
        menuItem -label "bool";
        menuItem -label "float";
        menuItem -label "rgb";
        menuItem -label "rgba";
        menuItem -label "vector";
        menuItem -label "point";
        menuItem -label "point2";
        menuItem -label "pointer";
        optionMenu -e -sl 4 $typeMenu;
        button -l "Add AOV" -c "createAOV()" -w 120 -h 20 -bgc 0.1 0.2 0.3;
    setParent..;
    
    separator -en 0 -h 30 -w 779;
    
    rowLayout -nc 3;
        columnLayout;
            rowLayout -nc 3;
                button -ann "在列表中选中要打断关联的物体，选中Triple" -l "Break" -c "disconnectSelFromTriple()" -w 65 -h 36 -bgc 0.1 0.2 0.2; 
                button -ann "列出所选的shape物体" -l "List Shape" -c "listNode(\"shape\")" -w 65 -h 36 -bgc 0.1 0.3 0.3;
                button -ann "在Triple列表中选中triple节点" -l "Connect2Triple" -c "addSelToTriple()" -w 120 -h 36 -bgc 0.1 0.4 0.4;
                setParent..;
                separator -h 5;
                
                $objScrollList = `textScrollList -ams on -sc "doubleClickInListCmd(\"shape\")" -h 300`;
            setParent..;
        columnLayout;
            rowLayout -nc 2;
                button -ann "列出场景中的Triple" -l "List Triple" -c "listNode(\"triple\")" -w 126 -h 36 -bgc 0.3 0.3 0.2;
                
                button -ann "在列表中选中要删除的Triple节点" -l "Del Triple" -c "delTripleNode()" -w 126 -h 36 -bgc 0.4 0.4 0.2;
                setParent..;
                separator -h 5;
                
                $tripleScrollList = `textScrollList -ams off -dcc "doubleClickInListCmd(\"triple\")" -h 300`;
            setParent..;
        columnLayout;
            rowLayout -nc 2;
                button -ann "选择要列出在列表中的材质球" -l "List Mat" -c "listNode(\"mat\")" -w 126 -h 36 -bgc 0.3 0.2 0.3;
                button -ann "选择要关联的物体,然后选择Triple,最后选择要关联的材质球" -l "ConnectMat2Triple" -c "addMatToSel()" -w 126 -h 36 -bgc 0.3 0.2 0.4;
                setParent..;
                separator -h 5;
                
                $matScrollList = `textScrollList -ams off -dcc "doubleClickInListCmd(\"mat\")" -h 300`;
            setParent..;
    showWindow tripleShadingConnector;
}

tripleShadingConnectorManager();
/*select $tripleNode;
string $name = connectionShape("ooo",0);
size($name); //字符串也可以查宽度

//找选择物体的对应index
getAttr -s ($tripleNode+".input");*/









