//YM_RebuildUIWindow - 重建常见窗口错误
global proc rebuildUI(int $switch)
{
    if($switch == 1)
    {
        deleteUI unifiedRenderGlobalsWindow;
    }
    else
    {
        deleteUI AEWindow;
    }
    buildNewSceneUI;
}

global proc deleteRenderView()
{
    string $scriptedPanels[] = `getPanel -type scriptedPanel`;
    string $temp = "";
    for($scriptedPanel in $scriptedPanels)
    {
        if(size(`match "renderWindow" $scriptedPanel`))
        {
            $temp = $scriptedPanel;
            break;
        }
    }
	
    catchQuiet(`deleteUI -panel $temp`);
}

global proc repairExpression()
{
	optionVar -iv "EEexprEdTextEditor" 1;
	optionVar -rm "EEexprEdTextEditorHold";
}


global proc rebuildUIWindow()
{
    if(`window -ex rebuildUIWindow`)
    {
        deleteUI rebuildUIWindow;
    }
    
    window -t "rebuildSettings" -w 120 -h 175 -sizeable off rebuildUIWindow;
    columnLayout;
    separator -h 2;
    button -l "Delete Render Settings" -w 120 -h 65 -c "rebuildUI(1)" -bgc 0.5 0.3 0.25;
    separator -h 2;
    button -l "Delete Render View" -w 120 -h 45 -c "deleteRenderView()" -bgc 0.4 0.2 0.15;
    separator -h 5;
    button -l "Delete Attribute Editor" -enable off -w 120 -h 25 -c "rebuildUI(0)" -bgc 0.3 0.3 0.3;
    separator -h 5;
    button -l "Fix RenderLayer BUG" -w 120 -h 25 -c "fixRenderLayerOutAdjustmentErrors" -bgc 0.3 0.3 0.3;
    separator -h 2;
	button -l "Set Expr Editor" -w 120 -h 25 -c "repairExpression" -bgc 0.2 0.2 0.2;
	separator -h 2;
	
    if(`window -ex rebuildUIWindow`)
    {
        catchQuiet(`windowPref -r rebuildUIWindow`);
    }
    showWindow rebuildUIWindow;
}

rebuildUIWindow();
